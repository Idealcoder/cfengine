# @brief Build and install a program from git
#
# Using the latest tag from a git url, this makes a deb package in 
# /usr/share/debs/$(name)-$(version).deb using 'build.sh', and installs it. 
#
# Package is automatically rebuilt and upgraded when a new git tag is released. 
# Binaries are protected by 'unpriv.sh' from being accidentally run by a 
# privileged user.

# @param name	name of the program
# @param url	git url of the program
#
# **Example:**

# ```cf3
# methods:
#  "build" usebundle => build("mcrcon", "https://github.com/Tiiffi/mcrcon.git");
# ```
bundle agent build(name, url) {
  vars:
    "latest_version" string => 
      execresult("/usr/local/bin/git-latest-tag $(url)", "noshell");

    "current_version" string => 
      execresult("dpkg -s $(name) | grep Version | awk '{print $2}'", "useshell");

  classes:
    "new_version" not => strcmp("$(latest_version)","$(current_version)");

  commands:
    new_version::
      "/usr/bin/env"
        contain => contain_nobody;

      "/usr/bin/env --ignore-environment /usr/local/bin/build-from-git $(url) /usr/share/deb/$(name)-$(latest_version).deb"
        contain => contain_nobody;

      "/usr/bin/apt-get install /usr/share/deb/$(name)-$(latest_version).deb";

  files:
      "/usr/local/bin/build-from-git"
        create => "true",
        perms => executable("root"),
        edit_template => "$(this.promise_dirname)/build-from-git.tmpl",
        template_method => "mustache";

      "/usr/local/bin/git-latest-tag"
        create => "true",
        perms => executable("root"),
        edit_template => "$(this.promise_dirname)/git-latest-tag.tmpl",
        template_method => "mustache";

      "/usr/local/bin/unpriv-only-protect"
        create => "true",
        perms => executable("root"),
        edit_template => "$(this.promise_dirname)/unpriv-only-protect.tmpl",
        template_method => "mustache";

  reports:
    "latest_version: $(latest_version),  current_version: $(current_version)";
}

body contain contain_nobody {
  useshell => "useshell";
  exec_owner => "nobody";
  exec_group => "nobody";
}
