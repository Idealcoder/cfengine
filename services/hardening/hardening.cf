bundle agent hardening {
  vars:
    "pkgs" slist => { 
                    # use isolated TMP directories
                      "libpam-tmpdir"

		    # apparmor
		    , "apparmor"
		    , "apparmor-profiles"
		    , "apparmor-profiles-extra"

		    # check covered by security updates
		    , "debian-security-support"
		    };
  packages:
    "$(pkgs)"
      policy => "present";

  files:
    "/etc/login.defs"
      comment => "make home directories for new users private",
      edit_line => replace_or_add("UMASK.*","UMASK		026");

    "/etc/adduser.conf"
      comment => "make home directories for new users private",
      edit_line => replace_or_add("DIR_MODE.*", "DIR_MODE=0751");

    "/etc/cron.allow"
      create => "true",
      acl => readable,
      comment => "restrict cron to root user",
      edit_line => lines_present("root");

    "/root/.profile"
      create => "true",
      acl => readable,
      comment => "make root sessions timeout",
      edit_line => lines_present("TMOUT=600");
}
