bundle agent minecraft {
  vars:
    "user" string => "minecraft";
    "version" string => "1.16.4";

  methods:
      "minecraft_mcrcon" usebundle => minecraft_mcrcon;
      "minecraft_systemd" usebundle => minecraft_systemd;

  classes:
    "user_exists" expression => userexists("$(user)");
    "jar_exists" expression =>  fileexists("/home/$(user)/server/server.jar");

  packages:
    "default-jre-headless"
      policy => "present",
      classes => if_ok("jre_installed");

  commands:
    !user_exists::
      "/usr/sbin/adduser"
        arglist => { "--quiet"
	           , "--shell /bin/zsh"
		   , "--add_extra_groups"
		   , "--disabled-login"
		   , "--gecos ''"
		   , "$(user)"},
	classes => if_ok("user_exists");

    !jar_exists::
      "/usr/bin/wget"
        arglist => { "--quiet"
	           , " --output-document /home/$(user)/server/server.jar"
		   , "https://papermc.io/api/v1/paper/$(version)/latest/download"};

  files:
    user_exists::
      "/home/$(user)/backups/."
        create => "true",
        perms => readable($(user));

      "/home/$(user)/tools/."
        create => "true",
        perms => readable($(user));

      "/home/$(user)/server/."
        create => "true",
        perms => readable($(user));

      # TODO requires 2nd pass for permissions to update
      "/home/$(user)/server/server.jar"
        create => "false",
        perms => executable($(user));

      "/home/$(user)/server/eula.txt"
        create => "true",
        perms => readable($(user)),
        edit_line => lines_present("eula=true");


}

# TODO
# - server.properties configure RCON
# - firewall
# - backups
