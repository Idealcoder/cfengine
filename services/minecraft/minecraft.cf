bundle agent minecraft {
  vars:
    "user" string => "minecraft";
    "version" string => "1.16.4";

  classes:
    "user_exists" expression => userexists("$(user)");
    "jar_exists" expression =>  fileexists("/home/$(user)/server/server.jar");

  packages:
    "default-jre-headless"
      policy => "present",
      classes => if_ok("jre_installed");

  commands:
    !user_exists::
      "/usr/sbin/adduser"
        arglist => { "--quiet"
	        , "--shell /bin/zsh"
		, "--add_extra_groups"
		, "--disabled-login"
		, "--gecos ''"
		, "$(user)"},
	classes => if_ok("user_exists");

    !jar_exists::
      "/usr/bin/wget -O /home/$(user)/server/server.jar https://papermc.io/api/v1/paper/$(version)/latest/download";

  files:
    # workaround, mcrcon needs directory to exist
    # for 'make install' to run correctly
    any::
      "/usr/local/share/man/man1/."
        create => "true",
        perms => readable("root"),
	classes => if_ok("build_ready");

    user_exists::
      "/home/$(user)/backups/."
        create => "true",
        perms => readable($(user));

      "/home/$(user)/tools/."
        create => "true",
        perms => readable($(user));

      "/home/$(user)/server/."
        create => "true",
        perms => readable($(user));

      "/home/$(user)/server/server.jar"
        create => "false",
        perms => executable($(user));

  methods:
    build_ready::
      "build" usebundle => build("mcrcon", "https://github.com/Tiiffi/mcrcon.git");
}
