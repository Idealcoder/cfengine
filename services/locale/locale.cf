bundle agent locale {
  vars:
    "locale" string => "en_GB.UTF-8 UTF-8";

  methods:
    "gen_locale"
      usebundle => gen_locale("$(locale)");
    "set_locale"
      usebundle => set_locale("$(locale)");
}

bundle agent gen_locale(locale) {
  files:
    "/etc/locale.gen"
      edit_line => replace_or_add(".*$(locale).*","$(locale)"),
      classes => if_repaired("locale_gen");

  commands:
    locale_gen::
      "/usr/sbin/locale-gen";

  reports:
    locale_gen::
      "regenerated locales";
}

bundle agent set_locale(locale) {
  vars:
    "locale_single" string => nth(string_split("$(locale)"," ",99), 0);
    "locales_list" string => execresult("/usr/bin/locale -a","noshell");
    "locale_current" string => readfile("/etc/default/locale");

  classes:
    "locale_set"       expression => regcmp(".*$(locale_single).*", "$(locale_current)");
    "locale_available" expression => regcmp(".*$(locale_single).*", "$(locales_list)");

  commands:
    locale_available&!locale_set::
      "/usr/sbin/update-locale LANG=$(locale_single)";

  reports:
   "locale_single $(locale_single)";
   "locales_list $(locales_list)";
   "locale_current $(locale_current)";
   locale_set::
     "locale is set";
   locale_available::
     "locale is available";
}
