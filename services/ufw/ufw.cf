bundle agent ufw {
  packages:
    "ufw"
      policy => "present",
      classes => if_ok("ufw_present");

  vars:
    ufw_present::
      "ufw_status" string => execresult("/usr/sbin/ufw status", "noshell");

  classes:
    ufw_present::
      "ufw_active"   
        and => {"ufw_present", strcmp("ufw_status", "Status: active"  )};
      "ufw_inactive"
        and => {"ufw_present", strcmp("ufw_status", "Status: inactive")};
      
  files:
    ufw_present::
      # same rules for ipv4 and ipv6
      "/etc/ufw/user.rules"
        edit_line => rules("ufw","0.0.0.0"),
        classes => if_repaired("reload_ufw");
      "/etc/ufw/user6.rules"
        edit_line => rules("ufw6", "::"),
        classes => if_repaired("reload_ufw");

  commands:
    ufw_inactive::
      "/usr/sbin/ufw enable"
        classes => if_ok("ufw_active");
    
    ufw_active.reload_ufw::
      "/usr/sbin/ufw reload";

  reports:
    ufw_active.reload_ufw::
      "ufw rules updated and reloaded";
}

bundle edit_line rules(type, zero) {
  insert_lines:
    "$(this.promise_dirname)/rules.tmpl"
      insert_type => "file_preserve_block",
      expand_scalars => "true",
      location => loc;
}

body location loc {
  select_line_matching => "### RULES ###";
  before_after => "after";
}
