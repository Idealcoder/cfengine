bundle agent apt(release) {
  vars:
    "repos" string => "main contrib";

    is_stable_flag::
      "is_stable" string => "true";

    !is_stable_flag::
      "is_stable" string => "false";

  classes:
    "testing" expression => strcmp("$(release)","testing");
    "unstable" expression => strcmp("$(release)","unstable");
    "experimental" expression => strcmp("$(release)","experimental");
    "is_stable_flag" expression => "!(testing|unstable|experimental)";

  files:
    "/etc/apt/apt.conf"
      create => "true",
      perms => readable("root"),
      edit_template => "$(this.promise_dirname)/apt.conf.tmpl",
      classes => if_repaired("clear_apt_lists");

    "/etc/apt/sources.list"
      create => "true",
      perms => readable("root"),
      edit_template => "$(this.promise_dirname)/sources.list.tmpl",
      template_method => "mustache",
      template_data => '{
        "release": "$(release)",
	"repos": "$(repos)",
	"is_stable": $(is_stable)
      }';
      # TODO - run apt update when sources change.

    "/etc/apt/sources.list.d/backports.list"
      delete => tidy;

  commands:
    clear_apt_lists::
      # need to use /bin as bundle runs before usrmerge is installed
      "/bin/rm -rf /var/lib/apt/lists";
      "/usr/bin/apt-get update --quiet";

  packages:
    "apt-listchanges"
      policy => "present";
}
