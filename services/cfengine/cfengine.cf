bundle agent cfengine {
  files:
    "$(sys.workdir)/modules"
      comment => "copy over module files as they are in the wrong place",
      copy_from => local_cp("/usr/share/cfengine3/modules"),
      depth_search => recurse("inf");

    "/etc/systemd/system/cf-agent-hourly.service"
      create => "true",
      perms => readable("root"),
      edit_template => "$(this.promise_dirname)/cf-agent-hourly.service.tmpl",
      classes => if_repaired("daemon_reload");

    "/etc/systemd/system/cf-agent-hourly.timer"
      create => "true",
      perms => readable("root"),
      edit_template => "$(this.promise_dirname)/cf-agent-hourly.timer.tmpl",
      classes => if_repaired("daemon_reload");

    "/usr/local/bin/cf-mailresult"
      create => "true",
      perms => executable("root"),
      edit_template => "$(this.promise_dirname)/cf-mailresult.tmpl",
      template_method => "mustache";

  commands:
    daemon_reload::
      "/bin/systemctl daemon-reload";

  services:
    "cf-agent-hourly.timer"
      service_policy => "start";

  reports:
    daemon_reload::
      "systemctl daemon-reload";

}
