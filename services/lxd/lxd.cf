bundle agent lxd {
  packages:
    "snapd"
      policy => "present",
      classes => if_ok("snapd_present");

  vars:
    snapd_present::
      "snaps_installed" string => execresult("/usr/bin/snap list", "noshell");

  classes:
    snapd_present::
      "snap_core_missing" not => regcmp("(.*)core(.*)","$(snaps_installed)");
      "snap_lxd_missing"  not => regcmp("(.*)lxd(.*)","$(snaps_installed)");
      "lxd_present" expression => "!snap_core_missing&!snap_lxd_missing";

  commands:
    # install core snap before lxd to avoid a bug in snapd, complaining about
    # the version of snapd being too old for lxd.
    snapd_present&snap_core_missing::
      "/usr/bin/snap install core";

    snapd_present&!snap_core_missing&snap_lxd_missing::
      "/usr/bin/snap install lxd"
        classes => if_ok("lxd_present");

    lxd_init_preseed::
      "cat /usr/local/etc/lxd/preseed.yaml | /snap/bin/lxd init --preseed"
        contain => in_shell;

  files:
    lxd_present::
      "/usr/local/etc/lxd/."
        create => "true",
	perms => readable("root");

      "/usr/local/etc/lxd/preseed.yaml"
        create => "true",
        perms => readable("root"),
        edit_template => "$(this.promise_dirname)/preseed.yaml",
        classes => if_repaired("lxd_init_preseed");

  reports:
    lxd_init_preseed::
      "lxd updated with new preseed file";
}

# TODO - add admin user to lxd group

# TODO - make everything install in a single pass

# TODO - configure firewall to allow dhcp & dns.
# sudo ufw allow in on lxdbr0 to any port 67 proto udp
# sudo ufw allow in on lxdbr0 to any port 53
# ufw route allow in on lxdbr0 out on wlan0
# and NAT, see https://ubuntu.com/server/docs/security-firewall

# don't let lxd touch iptables
# lxc network set lxdbr0 ipv4.nat false
# lxc network set lxdbr0 ipv6.nat false
# lxc network set lxdbr0 ipv6.firewall false
# lxc network set lxdbr0 ipv4.firewall false


# MAAS port forward
# https://discuss.linuxcontainers.org/t/forward-port-80-and-443-from-wan-to-container/2042
